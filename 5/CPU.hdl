// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

    // Aレジスタ
    And(a=instruction[15], b=instruction[5], out=c1);
    Not(in=instruction[15], out=c2);
    Or(a=c2, b=c1, out=loadA);
    Mux16(a=instruction, b=OutFromALU, sel=instruction[15], out=ToA);
    ARegister(in=ToA, load=loadA, out=FromAreg, out[0..14]=addressM);
    
    // Dレジスタ
    And(a=instruction[15], b=instruction[4], out=loadD);
    DRegister(in=OutFromALU, load=loadD, out=ToALU2);

    // Mレジスタ
    And(a=instruction[15], b=instruction[3], out=writeM);

    // ALU
    Mux16(a=FromAreg, b=inM, sel=instruction[12], out=ToALU);
    ALU(x=ToALU2, y=ToALU, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=OutFromALU, zr=zr, ng=ng);

    // PC
    And(a=ng, b=instruction[2], out=PCload1);
    And(a=zr, b=instruction[1], out=PCload2);
    Not(in=ng, out=Notng);
    Not(in=zr, out=Notzr);
    And(a=Notng, b=Notzr, out=plus);
    And(a=plus, b=instruction[0], out=PCload3);
    Or(a=PCload3, b=PCload2, out=m1);
    Or(a=m1, b=PCload1, out=m2);
    And(a=m2, b=instruction[15], out=PCload);
    PC(in=FromAreg, reset=reset, load=PCload, inc=true, out[0..14]=pc);
}